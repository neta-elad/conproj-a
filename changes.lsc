# +--------------+
# | ~ Features ~ |
# +--------------+
Feature voicing                 (voiceless, voiced)
Feature place                   (labial, dental, alveolar, postalveolar, retroflex, palatal, velar, labiovelar, uvular, pharyngeal, glottal)
Feature manner                  (nasal, stop, trill, tap, fricative, affricate, approximant)
Feature type                    (*consonant, vowel)
Feature +lateral

Feature +labialized
Feature +palatalized
Feature +velarized
Feature +pharyngealized
Feature +aspirated

Feature height                  (low, mid, high)
Feature frontness               (front, central, back)

Feature +round
Feature +long
Feature +nasalized
Feature +accented

Feature (syllable) stress       (*unstressed, primary, secondary)
Feature (syllable) rhyme        (open, semiopen, closed)

# +----------------+
# | ~ Diacritics ~ |
# +----------------+
Diacritic ʷ [+labialized]       (floating)
Diacritic ʲ [+palatalized]      (floating)
Diacritic ˠ [+velarized]        (floating)
Diacritic ˤ [+pharyngealized]   (floating)
Diacritic ʰ [+aspirated]        (floating)

Diacritic ː [+long]             (floating)
Diacritic  ̃ [+nasalized]        (floating) (first)
Diacritic  ́ [+accented]         (floating)

Diacritic ˈ [primary]           (before)
Diacritic ˌ [secondary]         (before)

Diacritic ˚ [open]
Diacritic ˘ [semiopen]
Diacritic ˙ [closed]

# +----------------+
# | ~ Consonants ~ |
# +----------------+
# Labial
Symbol m [voiced labial nasal]
Symbol p [voiceless labial stop]
Symbol b [voiced labial stop]
Symbol ʙ [voiced labial trill]
Symbol ⱱ [voiced labial tap]
Symbol f [voiceless labial fricative]
Symbol v [voiced labial fricative]
Symbol p͡f [voiceless labial affricate]
Symbol b͡v [voiced labial affricate]
Symbol ʋ [voiced labial approximant]

# Labiovelar
Symbol ʍ [voiceless labiovelar fricative]
Symbol w [voiced labiovelar approximant]

# Dental
Symbol t̪ [voiceless dental stop]
Symbol d̪ [voiced dental stop]
Symbol θ [voiceless dental fricative]
Symbol ð [voiced dental fricative]

# Alveolar
Symbol n [voiced alveolar nasal]
Symbol t [voiceless alveolar stop]
Symbol d [voiced alveolar stop]
Symbol r [voiced alveolar trill]
Symbol ɾ [voiced alveolar tap]
Symbol s [voiceless alveolar fricative]
Symbol z [voiced alveolar fricative]
Symbol ɬ [voiceless alveolar +lateral fricative]
Symbol ɮ [voiced alveolar +lateral fricative]
Symbol t͡s [voiceless alveolar affricate]
Symbol d͡z [voiced alveolar affricate]
Symbol ɹ [voiced alveolar approximant]
Symbol l [voiced alveolar +lateral approximant]

# Post-alveolar
Symbol t̠ [voiceless postalveolar stop]
Symbol ʃ [voiceless postalveolar fricative]
Symbol ʒ [voiced postalveolar fricative]
Symbol t͡ʃ [voiceless postalveolar affricate]
Symbol d͡ʒ [voiced postalveolar affricate]

# Retroflex
Symbol ɳ [voiced retroflex nasal]
Symbol ʈ [voiceless retroflex stop]
Symbol ɖ [voiced retroflex stop]
Symbol ɽ [voiced retroflex tap]
Symbol ʂ [voiceless retroflex fricative]
Symbol ʐ [voiced retroflex fricative]
Symbol ɻ [voiced retroflex approximant]
Symbol ɭ [voiced retroflex +lateral approximant]

# Palatal
Symbol ɲ [voiced palatal nasal]
Symbol c [voiceless palatal stop]
Symbol ɟ [voiced palatal stop]
Symbol ç [voiceless palatal fricative]
Symbol ʝ [voiced palatal fricative]
Symbol j [voiced palatal approximant]
Symbol ʎ [voiced palatal +lateral approximant]
Symbol ʎ̝̊ [voiceless palatal +lateral fricative]

# Velar
Symbol ŋ [voiced velar nasal]
Symbol k [voiceless velar stop]
Symbol g [voiced velar stop]
Symbol x [voiceless velar fricative]
Symbol ɣ [voiced velar fricative]
Symbol ɰ [voiced velar approximant]
Symbol ʟ [voiced velar +lateral approximant]

# Uvular
Symbol ɴ [voiced uvular nasal]
Symbol q [voiceless uvular stop]
Symbol ɢ [voiced uvular stop]
Symbol ʀ [voiced uvular trill]
Symbol χ [voiceless uvular fricative]
Symbol ʁ [voiced uvular fricative]

# Pharyngeal
Symbol ʡ [voiceless pharyngeal stop]
Symbol ħ [voiceless pharyngeal fricative]
Symbol ʕ [voiced pharyngeal fricative]

# Glottal
Symbol ʔ [voiceless glottal stop]
Symbol h [voiceless glottal fricative]
Symbol ɦ [voiced glottal fricative]

# +------------+
# | ~ Vowels ~ |
# +------------+
# High
Symbol i [high front -round vowel]
Symbol y [high front +round vowel]
Symbol ɨ [high central -round vowel]
Symbol ʉ [high central +round vowel]
Symbol ɯ [high back -round vowel]
Symbol u [high back +round vowel]

# Mid
Symbol e [mid front -round vowel]
Symbol ø [mid front +round vowel]
Symbol ə [mid central -round vowel]
Symbol ɵ [mid central +round vowel]
Symbol ɤ [mid back -round vowel]
Symbol o [mid back +round vowel]

# Low
Symbol ɛ [low front -round vowel]
Symbol œ [low front +round vowel]
Symbol a [low central -round vowel]
Symbol ɶ [low central +round vowel]
Symbol ɑ [low back -round vowel]
Symbol ɒ [low back +round vowel]

# +-------------+
# | ~ Classes ~ |
# +-------------+
Class rhotics       {r, ɾ, ɹ, ɻ, ʀ, ʁ}
Class sibilants     {s, z, ʃ, ʒ}
Class glides        {w, j}

# +--------------+
# | ~ Elements ~ |
# +--------------+
Element liquids     {@rhotics, [+lateral approximant]}
Element sonorants   {[nasal], @liquids}
Element obstruents  {[stop], [fricative], [affricate]}
Element nonglides   [consonant]&!@glides
Element palatalizes {[+palatalized], j} 
Element labializes  {[+labialized], w}

# +-------------+
# | ~ Changes ~ |
# +-------------+
syllables:
    [consonant]? [vowel]


stress-pattern:
    <syl> => [primary] / _ <syl> $
else:
    <syl> => [primary] / $ _ $


proto:
    unchanged


schwa-assimilation:
    [mid central vowel] [vowel $frontness $height $round] => [$frontness $height $round] *


post-stress-word-final-vowel-distinction-loss [vowel]:
    [vowel] => [mid central -round] / [primary] _ $


hiatus-resolution:
    [vowel] => * / [vowel] _


high-vowels-become-glides:
    {i, u} * .  => {j, w} ə .


syllables:
    [consonant]? @glides? [vowel]


glides-attach-to-consonants:
    [consonant] w => [+labialized] *
    [consonant] j => [+palatalized] *


syllables:
    [consonant]? [vowel]


early:
    unchanged


vowel-loss-between-stops-and-fricatives:
    [unstressed vowel] => * / [stop] _ [fricative]
then:
    [unstressed vowel] => * / [fricative] _ [stop] [vowel] // [consonant] [fricative] _
then:
    [fricative]$1 [stop]$2 => $2 $1 / . _


syllables:
    [consonant]? [fricative]? [vowel]


vowel-loss-between-obstruents-and-liquids:
    [unstressed vowel] => * / @obstruents _ @liquids



syllables:
    [consonant]? [fricative]? @liquids? [vowel]


palatalization-spreads:
    [consonant] => [+palatalized] / {_ @palatalizes, @palatalizes _}


labialization-spreads:
    [consonant] => [+labialized] / {_ @labializes, @labializes _}


middle:
    unchanged


palatalization-fronts-vowel:
    [vowel] => [front] / {_ @palatalizes, @palatalizes _}


labialization-rounds-vowel:
    [vowel] => [+round] / {_ @labializes, @labializes _}


vowel-polarization propagate:
    [mid vowel] => [high]
    [central vowel] => [back]


palatalization-simplifies:
    [labial +palatalized] => [*palatalized]
    @obstruents&[alveolar +palatalized] => [postalveolar *palatalized]
    @sonorants&!@rhotics&[alveolar +palatalized] => [palatal *palatalized]
    [velar +palatalized] => [palatal *palatalized]
    [glottal +palatalized] => [*palatalized]
    @rhotics => [*palatalized]


labialization-simplifies:
    [+labialized] => [*labialized]


first-word-final-vowel-loss:
    [unstressed vowel] => * / _ $
then propagate:
    [stop]$1 [fricative]$2 => $2 $1 / _ @liquids? $
    @obstruents$1 @liquids$2 => $2 $1 / _ [stop]? $


syllables:
    [stop]? [fricative]? @liquids? [vowel] @liquids? [fricative]? [consonant]? // [vowel] _
    @obstruents? @liquids? [vowel] @liquids? [fricative]? [consonant]? // [vowel] _
    [consonant]? [vowel] @liquids? [fricative]? [consonant]?


late:
    unchanged


h-devoices:
    [alveolar stop] => [dental] / {_ h, h _}
then:
    [consonant]&!@rhotics h => [voiceless fricative] *
    h [consonant]&!@rhotics => * [voiceless fricative]
then:
    [consonant]&@rhotics h => [voiceless +lateral fricative] *
    h [consonant]&@rhotics => * [voiceless +lateral fricative]
then:
    [dental stop] => [alveolar]


word-final-weak-consonants-loss:
    {h, j, w} => * / _ $


lateral-fricatives-merge:
    [+lateral fricative] => [alveolar]


vowel-loss-near-stress:
    [unstressed vowel] => * / [primary vowel] [consonant] _ [consonant] [vowel]
    [unstressed vowel] => * / [vowel] [consonant] _ [consonant] [primary vowel]


h-fortition:
    [glottal fricative] => [velar] / {[consonant] _ [back vowel], [back vowel] _ [consonant]}
    [glottal fricative] => [palatal] / {[consonant] _ [front vowel], [front vowel] _ [consonant]}


palatal-assimilation:
    @obstruents&{[alveolar], [postalveolar]} => [palatal] / {[palatal] _, _ [palatal]} // {[palatal] . _, _ . [palatal]}


palatal-obstruents-become-velar:
    # in coda positions
    @obstruents&[palatal] => [velar] / _ [consonant]* .

    # and in clusters
    @obstruents&[palatal] => [velar] / . _ [consonant]+ [vowel]
then:
    @obstruents&[palatal] => [velar] / . [consonant]+ _ [vowel]


palatal-sonorants-become-alveolar:
    # in coda positions
    @sonorants&[palatal] => [alveolar] / _ [consonant]* .

    # and in clusters
    @sonorants&[palatal] => [alveolar] / . _ [consonant]+ [vowel]
then:
    @sonorants&[palatal] => [alveolar] / . [consonant]+ _ [vowel]


postalveolar-stop-simplifies:
    [postalveolar stop] => [alveolar] / {[consonant] _, _ [consonant]}
then:
    [postalveolar stop] => [affricate]


semivowels-dissimilation:
    [vowel] => [low] / [palatal] _


romanizer-phonetic:
    unchanged


accent-stressed-vowels [vowel]:
    [primary] => [+accented] // _ $


syllables:
    clear


modern:
    unchanged


romanizer:
    kç => k'hy
    cç => chy
    tç => t'hy
    sç => s'hy
    sh => s'h
    th => t'h
    kh => k'h
    nj => n'y
    kj => k'y
    lj => l'y
    t͡ʃj => ch'y

    ŋ => ng
    x => kh
    ʍ => hw

    ɲ => ny
    c => ky
    ç => hy
    ʎ => ly
    j => y

    t͡ʃ => ch
    ʃ => sh

    ɾ => r
    ɬ => hl
    θ => th

    ɯ => eu
    ɛ => e
    œ => oe
    ɑ => a
    ɒ => o
